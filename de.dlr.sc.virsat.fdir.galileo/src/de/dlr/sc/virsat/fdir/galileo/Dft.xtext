/*******************************************************************************
 * Copyright (c) 2008-2019 German Aerospace Center (DLR), Simulation and Software Technology, Germany.
 *
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/
 
grammar de.dlr.sc.virsat.fdir.galileo.Dft with org.eclipse.xtext.common.Terminals

generate dft "http://www.dlr.de/sc/virsat/fdir/galileo/Dft"

GalileoDft:
	'toplevel' root=[GalileoFaultTreeNode|STRING] ';'
	(gates+=GalileoGate ';' | basicEvents+=GalileoBasicEvent ';')*
;

GalileoGate returns GalileoFaultTreeNode:
	name=STRING type=GalileoNodeType (children+=[GalileoFaultTreeNode|STRING])*
;

GalileoBasicEvent returns GalileoFaultTreeNode:
	name=STRING 'lambda' '=' lambda=Float ('dorm' '=' dorm=Float)? (repairActions+=GalileoRepairAction)*
;

GalileoRepairAction:
	'repair' '=' repair=Float (name=STRING)? ('observations' observartions+=[GalileoFaultTreeNode|STRING]*)?
;

GalileoNodeType:
	Named | Parametrized | Observer
;

Named returns GalileoNodeType:
	{Named} typeName=('and' | 'or' |  XOFY | 'pand' | 'pand_i' | 'por' | 'por_i' | 'sand' | 'hsp' | 'wsp' | 'csp' | 'seq' | 'fdep')
;

Observer returns GalileoNodeType: 
	{Observer} 'observer' (observables+=[GalileoFaultTreeNode|STRING])* 'obsRate' '=' observationRate=Float 
;

Parametrized returns GalileoNodeType:
	{Parametrized} typeName = ('rdep' | 'delay') '=' parameter=Float
;


Float:
	'-'? INT ('.' INT)?	('e' '-'? INT)?
;

terminal XOFY: INT 'of' INT;
